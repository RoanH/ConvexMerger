plugins{
	id 'java-library'
	id 'eclipse'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'edu.sc.seis.launch4j' version '2.5.1'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['assets']
		}
	}
	test{
		java{
			srcDir 'test'
		}
		resources{
			srcDir 'test'
		}
	}
}

eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = 'v1.1'//don't forget Constants.java
shadowJar.archiveName = 'ConvexMerger-' + version + '.jar'

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	mavenCentral();
}

dependencies{
	implementation 'dev.roanh.util:util:2.1'
	testImplementation(platform('org.junit:junit-bom:5.8.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
}

test{
	useJUnitPlatform()
	dependsOn 'cleanTest'
	
	testLogging{
		events "passed", "skipped", "failed"
	}
}

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

application{
	mainClassName = 'dev.roanh.convexmerger.Main'
}

launch4j{
	jarTask = project.tasks.shadowJar
	mainClassName = application.mainClassName
	icon = "${projectDir}/icon.ico"
	jreMinVersion = project.sourceCompatibility
	bundledJrePath = "%JAVA_HOME%"
	outfile = project.name + '-' + project.version + '.exe'
	fileDescription = project.name
	version = project.version.substring(1)
	textVersion = project.version.substring(1)
	copyright = "Roan Hofland"
}